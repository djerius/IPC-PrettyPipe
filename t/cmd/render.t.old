#! perl

use strict;
use warnings;

use IPC::PrettyPipe::Cmd;
use IPC::PrettyPipe::DSL::Arg qw[ sep pfx ];
use Test::More;
use Test::Exception;

sub new { IPC::PrettyPipe::Cmd->new( cmd => shift(), ( @_ ? (args => \@_) : () )); }


sub run_methods {

    my $cmd = shift;

    while ( 1 ) {

        my ( $method, $args ) = ( shift, shift );
        last unless defined $method;
        $cmd->$method( @$args );
    }

    return;
}


sub run_test {

    my %args = @_;

    $args{render}  //= [];
    $args{methods} //= [];

    lives_and {

        my $cmd = new( @{ $args{new} } );

        run_methods( $cmd, @{ $args{methods} } );

        is_deeply( [ $cmd->render( @{ $args{render} } ) ], $args{expect} );
    }
    $args{desc};

    return;
}



my @tests = (

    {
        desc   => 'list',
        new    => [ 'ls', '-l', '-r' ],
        expect => [ 'ls', '-l', '-r' ],
    },

    {
        desc    => 'list, arg',
        new     => ['ls'],
        methods => [
            argpfx => ['-'],
            ffadd    => ['l'],
            ffadd    => ['r'],
        ],
        expect => [ 'ls', '-l', '-r' ],
    },

    {
        desc => 'array',
        new  => [ 'ls', [ -W => 80, -T => 3 ] ],
        expect => [ 'ls', ['-W', 80], ['-T', 3]  ],
    },

    {
        desc => 'arg hash',
        new  => [ 'ls' ],
        methods => [
                    argsep => [ ' ' ],
                    ffadd => [ { -W => 80 } ],
                   ],
        expect => [ 'ls', '-W 80' ],
    },

    {
        desc => 'array, arg',
        new  => ['ls'],
        methods =>
          [ ffadd => [ pfx '--', sep '=',
                       [ width => 80, tab => 3 ],
                     ],
          ],
        expect => [ 'ls', '--width=80', '--tab=3' ],
    },

    #  render args
    {
        desc => 'array, render pfx',
        new  => ['ls', [ -W => 80, -T => 3 ] ],
        render => [ sep => '=' ],
        expect => [ 'ls', '-W=80', '-T=3' ],
    },

    {
        desc => 'array, render pfx, hash',
        new  => ['ls', [ -W => 80, -T => 3 ] ],
        render => [ { sep => '=' } ],
        expect => [ 'ls', '-W=80', '-T=3' ],
    },

    {
        desc => 'array, render pfx',
        new  => ['ls', [ -W => 80, -T => 3 ] ],
        render => [ quote => 1, sep => '=' ],
        expect => [ 'ls', q['-W=80'], q['-T=3'] ],
    },


);


run_test( %$_ ) for @tests;


done_testing;
